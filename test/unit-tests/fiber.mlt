open Dune;;
open Stdune;;
open Fiber;;
open Fiber.O;;

let failing_fiber () =
  Fiber.yield ()
  >>= fun () ->
  raise Exit
;;

let long_running_fiber () =
  let rec loop n =
    if n = 0 then
      Fiber.return ()
    else
      Fiber.yield ()
      >>= fun () ->
      loop (n - 1)
  in
  loop 10
;;

let ivar : unit Fiber.Ivar.t = Fiber.Ivar.create ();;

let getter_fiber () =
  Fiber.Ivar.read ivar
;;

[%%expect{|
val failing_fiber : unit -> 'a t = <fun>
val long_running_fiber : unit -> unit t = <fun>
val ivar : unit Ivar.t = <abstr>
val getter_fiber : unit -> unit t = <fun>
|}]

Fiber.run (Fiber.collect_errors failing_fiber)
[%%expect{|
- : ('a, exn list) Stdune.result = Error [Exit]
|}]

let success = ref false;;

try
  Fiber.run (Fiber.collect_errors getter_fiber);
  ()
with Fiber.Never ->
  success := true
;;
[%%expect{|
val success : bool ref = {contents = false}
- : unit = ()
|}]

!success;;
[%%expect{|
- : bool = true
|}]

Fiber.run (
  Fiber.collect_errors (fun () -> (
    failing_fiber ()
    >>= fun () ->
    failing_fiber ())))
;;

[%%expect{|
- : ('a, exn list) Stdune.result = Error [Exit]
|}]

Fiber.run (
  Fiber.collect_errors (fun () -> Fiber.with_error_handler failing_fiber ~on_error:ignore))
[%%expect{|
- : ('a, exn list) Stdune.result = Error []
|}]

Fiber.run (
  Fiber.collect_errors (fun () -> Fiber.with_error_handler failing_fiber ~on_error:ignore)
  >>| fun result -> "")
[%%expect{|
- : string = ""
|}]

Fiber.run (
  Fiber.collect_errors
    (fun () -> Fiber.fork_and_join failing_fiber long_running_fiber))
[%%expect{|
- : ('a * unit, exn list) Stdune.result = Error [Exit]
|}]

Fiber.run (
  Fiber.fork_and_join
    (fun () ->
       Fiber.collect_errors failing_fiber
       >>| fun _ -> "")
    long_running_fiber)
[%%expect{|
- : string * unit = ("", ())
|}]

let flag_set = ref false;;
let never_raised = ref false;;

try
  Fiber.run (
    Fiber.fork_and_join
      getter_fiber
      (fun () ->
         Fiber.collect_errors failing_fiber
         >>= fun _ ->
         long_running_fiber ()
         >>= fun _ -> Fiber.return (flag_set := true)));
  ()
with Fiber.Never ->
  never_raised := true
;;
[%%expect{|
val flag_set : bool ref = {contents = false}
val never_raised : bool ref = {contents = false}
- : unit = ()
|}]

!flag_set && !never_raised;;
[%%expect{|
- : bool = true
|}]

let flag_set = ref false;;
let never_raised = ref false;;

let forking_fiber () =
  Fiber.parallel_map [1;2;3;4;5]
    ~f:(fun x ->
      Fiber.yield ()
      >>= fun () ->
      if x mod 2 = 1 then
        Process.run Process.Strict ~env:Env.initial (Option.value_exn (Bin.which "true")) []
      else
        Process.run Process.Strict ~env:Env.initial (Option.value_exn (Bin.which "false")) [])
in
try
  Fiber.run (
    Fiber.fork_and_join
      getter_fiber
      (fun () ->
         Fiber.collect_errors forking_fiber
         >>= fun _ ->
         long_running_fiber ()
         >>= fun _ -> Fiber.return (flag_set := true)));
  ()
with Fiber.Never ->
  never_raised := true
;;
[%%expect{|
val flag_set : bool ref = {contents = false}
val never_raised : bool ref = {contents = false}
- : unit = ()
|}]

!flag_set && !never_raised;;
[%%expect{|
- : bool = true
|}]
